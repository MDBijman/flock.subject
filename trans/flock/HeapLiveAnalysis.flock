module examples/LiveVariables
signature

  sorts
    ID = string
    Expr
    Statement
    Start
    Function
    Return

  constructors
    IfThenElse : Expr * Statement * Statement -> Statement
    Assign : ID * Expr -> Statement
    DerefAssign : ID * Expr -> Statement
    Skip : Statement
    Block : list(Statement) -> Statement
    While : Expr * list(Statement) -> Statement
    Print : Expr -> Statement
    Root : Function -> Start
    Malloc : Expr
    Free : ID -> Statement
    Int : ID -> Expr
    Ref : ID -> Expr
    Deref : ID -> Expr
    True : Expr
    False : Expr
    Add : Expr * Expr -> Expr
    Sub : Expr * Expr -> Expr
    Gt : Expr * Expr -> Expr
    Gte : Expr * Expr -> Expr
    Ret : Expr -> Return
    Return : Statement
    Func : ID * list(Statement) * Return -> Function


control-flow rules

  root Root(s) = start -> s -> end

  Func(_, stmts, r) = entry -> stmts -> r -> exit

  Assign(n, e) =
    entry -> e -> this -> exit

  DerefAssign(n, e) =
    entry -> e -> this -> exit

  Skip() = entry -> exit
  
  Ret(e) = entry -> e -> this -> exit

  Block(stmts) = entry -> stmts -> exit

  IfThenElse(c, t, e) =
    entry -> c -> t -> exit,
             c -> e -> exit

  While(e, s) =
    entry -> e -> s -> e -> exit
    
  Add(e1, e2) = entry -> e1 -> e2 -> this -> exit
  Sub(e1, e2) = entry -> e1 -> e2 -> this -> exit
  Gt(e1, e2)  = entry -> e1 -> e2 -> this -> exit
  Gte(e1, e2) = entry -> e1 -> e2 -> this -> exit
    
  Print(e) =
    entry -> e -> this -> exit

  node Malloc()
  node Free(_)
  node Int(_)
  node True()
  node False()
  node Ref(_)
  node Deref(_)
  
properties
  live: MaySet[name]

property rules
  live(_.end) = {}
  
  live(Ref(n) -> next) = result
  where
    previous = live(next)
    current = {n}
    result = previous \/ current

  live(Deref(n) -> next) = result
  where
    previous = live(next)
    current = {n}
    result = previous \/ current
    
  live(DerefAssign(n, _) -> next) = result
  where
    previous = live(next)
    current = {n}
    result = previous \/ current
    
  live(Assign(n, e) -> next) = { m | m <- live(next), n != m }
  live(_ -> next) = live(next)