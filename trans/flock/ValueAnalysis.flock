module examples/Min
signature

  sorts
    ID = string
    Expr
    Statement
    Start
    Function
    Return

  constructors
    IfThenElse : Expr * Statement * Statement -> Statement
    Assign : ID * Expr -> Statement
    DerefAssign : ID * Expr -> Statement
    Skip : Statement
    Block : list(Statement) -> Statement
    While : Expr * list(Statement) -> Statement
    Print : Expr -> Statement
    Root : Function -> Start
    Malloc : Expr
    Free : ID -> Statement
    Int : ID -> Expr
    Ref : ID -> Expr
    Deref : ID -> Expr
    True : Expr
    False : Expr
    Add : Expr * Expr -> Expr
    Sub : Expr * Expr -> Expr
    Gt : Expr * Expr -> Expr
    Gte : Expr * Expr -> Expr
    Ret : Expr -> Return
    Return : Statement
    Func : ID * list(Statement) * Return -> Function


control-flow rules

  root Root(s) = start -> s -> end

  Func(_, stmts, r) = entry -> stmts -> r -> exit

  Assign(n, e) =
    entry -> e -> this -> exit

  DerefAssign(n, e) =
    entry -> e -> this -> exit

  Skip() = entry -> exit
  
  Ret(e) = entry -> e -> this -> exit

  Block(stmts) = entry -> stmts -> exit

  IfThenElse(c, t, e) =
    entry -> c -> t -> exit,
             c -> e -> exit

  While(e, s) =
    entry -> e -> s -> e -> exit
    
  Add(e1, e2) = entry -> e1 -> e2 -> this -> exit
  Sub(e1, e2) = entry -> e1 -> e2 -> this -> exit
  Gt(e1, e2)  = entry -> e1 -> e2 -> this -> exit
  Gte(e1, e2) = entry -> e1 -> e2 -> this -> exit
    
  Print(e) =
    entry -> e -> this -> exit

  node Malloc()
  node Free(_)
  node Int(_)
  node True()
  node False()
  node Ref(_)
  node Deref(_)
  
properties
  values: Environment

property rules
  values(_.end) = {[Map[name, Value]]}
  values(prev -> Assign(n, Int(i))) = { k |-> v | (k |-> v)  <- values(prev), k != n } \[Environment]/ {n |-> Const(i)}
  values(prev -> Assign(n, _))      = { k |-> v | (k |-> v)  <- values(prev), k != n } \[Environment]/ {n |-> Top()}
  values(prev -> _) = values(prev)

types
  ConstProp =
  | Top()
  | Const(int)
  | Bottom()
  
lattices
  Value where
    type = ConstProp
    bottom = Bottom()
    top = Top()
    lub(l, r) = match (l, r) with
      | (Top(), _) => Top()
      | (_, Top()) => Top()
      | (Const(i), Const(j)) => if i == j then Const(i) else Top()
      | (_, Bottom()) => l
      | (Bottom(), _) => r
      
  Environment where
    type = Map[name, Value]
    bottom = Map[name, Value].bottom
    top = Map[name, Value].top
    lub(l, r) = Map[name, Value].lub(l, r)
    