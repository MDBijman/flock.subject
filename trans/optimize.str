module optimize

imports
  flock/api

signature
  constructors
    Const : INT -> Const

strategies

  fixpoint-opt(s|0) = id
  fixpoint-opt(s|bound): p -> <s < (fixpoint-opt(s | <dec> bound)) + id> p
  where <not(equal(|0))> bound

strategies 

  optimize-program: p -> p'
  where p' := <optimize-program'; flock-debug-graph> p

  optimize-program': p -> p''
  with
    p' := <flock-add-id> p
  ; <flock-analyse-program> p'
  ; <flock-debug-graph> ()
  ; <debug> <run-time>
  ; pf  := <forward-pass>  p'
  ; <debug> <run-time>
  ; pb  := <backward-pass> pf
  ; <debug> <run-time>
  ; p'' := <cleanup-pass>  pb
  ; <debug> <run-time>

  forward-pass = fixpoint-opt(forward-pass-single | 3000)
  forward-pass-single: p -> p'
  where 
   p' := <oncetd(opt-forward)> p
 ; <flock-update> p'
 
  opt-forward: p -> p''
  where 
    p'  := <forward-optimizations> p 
  ; p'' := <flock-replace-node(|p)> p'
 
  forward-optimizations  = fold-expressions + propagate-constant-read + replace-dead-if
 
  backward-pass = fixpoint-opt(backward-pass-single | 3000)
  backward-pass-single: p -> p'
  where
    p' := <oncetd(opt-backward)> p
  ; <flock-update> p'
  
  opt-backward: p -> p''
  where 
    p'  := <backward-optimizations> p 
  ; p'' := <flock-replace-node(|p)> p'
  
  backward-optimizations = remove-dead-store + remove-dead-heap-store
  
  
  cleanup-pass = fixpoint-opt(cleanup-pass-single | 1000)
  cleanup-pass-single: p -> p'
  where 
    p' := <oncetd(opt-cleanup)> p
  ; <flock-update> p' 
  
  opt-cleanup: p -> p'
  where 
    (p', dead-nodes) := <cleanup-optimizations> p
  ; <map(flock-remove-node)> dead-nodes
  
  cleanup-optimizations  = remove-skips + remove-empty-blocks
  
strategies

  fold-expressions: Add(Int(v1), Int(v2)) -> Int(sum)
  where 
    sum := <(string-to-int, string-to-int); addi; int-to-string> (v1, v2)
  ; <concat-strings; flock-debug> ["[fold-expressions] ", v1, " + ", v2, " -> ", sum]
  
  fold-expressions: Sub(Int(v1), Int(v2)) -> Int(sub)
  where
    sub := <(string-to-int, string-to-int); subti; int-to-string> (v1, v2)
  ; <concat-strings; flock-debug> ["[fold-expressions] ", v1, " + ", v2, " -> ", sub]

  fold-expressions: Gt(Int(v1), Int(v2)) -> res
  where
    res := <(string-to-int, string-to-int); gti < !True() + !False()> (v1, v2)
  ; <concat-strings; flock-debug> ["[fold-expressions] ", v1, " > ", v2]

  fold-expressions: Gte(Int(v1), Int(v2)) -> res
  where
    res := <(string-to-int, string-to-int); (gti + eq) < !True() + !False()> (v1, v2)
  ; <concat-strings; flock-debug> ["[fold-expressions] ", v1, " >= ", v2]
  
strategies

  propagate-constant-read: a@Ref(m) -> Int(<remove-id> v)
  where
    <flock-debug> a
  ; Const(v) := <flock-get-value(|m)> a
  ; <concat-strings; flock-debug> ["Const value ", m, ": ", v]

  remove-dead-store: a@Assign(n, _) -> Skip()
  where
    <not(flock-is-live(|n))> a
  ; <concat-strings; flock-debug> ["dead variable ", n]

  remove-dead-heap-store: a@DerefAssign(n, _) -> Skip()
  where
    <not(flock-is-live(|n))> a
  ; as := <get-alias-set(|n)> a 
  ; <map(!(<id>, a); not(flock-is-live)|)> as
  ; <concat-strings; flock-debug> ["dead heap store ", n]

  replace-dead-if: IfThenElse(True, t, e)  -> <remove-id> t
  where <flock-debug> "removing dead if"

  replace-dead-if: IfThenElse(False, t, e) -> <remove-id> e
  where <flock-debug> "removing dead if"

  replace-dead-if: IfThenElse(e, Skip(), Skip()) -> Skip()
  where <flock-debug> "removing dead if"


strategies

  remove-empty-blocks: Block(stmts) -> (Block(stmts'), empties)
  where
    stmts' := <retain-all(not(?Block([])))> stmts
  ; empties := <retain-all(?Block([]))> stmts
  ; <not(equal)> (stmts, stmts')
  ; <not(equal)> ([], empties)
  where <flock-debug> "removing empty blocks"

  remove-empty-blocks: Func(n, stmts, r) -> (Func(n, stmts', r), empties)
  where
    stmts' := <retain-all(not(?Block([])))> stmts
  ; empties := <retain-all(?Block([]))> stmts
  ; <not(equal)> (stmts, stmts')
  ; <not(equal)> ([], empties)
  where <flock-debug> "removing empty blocks"

  remove-skips: Block(stmts) -> (Block(stmts'), skips)
  where
    stmts' := <retain-all(not(?Skip))> stmts
  ; skips := <retain-all(?Skip)> stmts
  ; <not(equal)> (stmts, stmts')
  ; <not(equal)> ([], skips)
  where <flock-debug> "removing skips"
  
  remove-skips: Func(n, stmts, r) -> (Func(n, stmts', r), skips)
  where
    stmts' := <retain-all(not(?Skip))> stmts
  ; skips := <retain-all(?Skip)> stmts
  ; <not(equal)> (stmts, stmts')
  ; <not(equal)> ([], skips)
  where <flock-debug> "removing skips"

  remove-singleton-block: Block([s]) -> <remove-id> s
  where <flock-debug> "unwrapping statement from block"

strategies

  remove-id: s -> <bottomup(try(remove-single-id))> s
  remove-single-id: s {FlockNodeId(_)} -> <set-annotations> (s, [])
  